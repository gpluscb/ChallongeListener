plugins {
	// Apply the java-library plugin to add support for Java Library
	id 'java-library'
}

apply plugin: 'signing'
apply plugin: 'java'
apply plugin: 'maven'

group = "com.github.gpluscb"
archivesBaseName = "ChallongeListener"
version = "1.0.0-SNAPSHOT"

repositories {
	// mavenCentral to resolve dependencies and publish to
	mavenCentral()
	
	// Sonatype oss for snapshot
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}
}

dependencies {
	// This dependency is exported to consumers, that is to say found on their compile classpath.
	api 'org.apache.commons:commons-math3:3.6.1'
	
	// This dependency is used internally, and not exposed to consumers on their own compile classpath.
	implementation 'com.google.guava:guava:27.0.1-jre'
	
	// Use JUnit test framework
	testImplementation 'junit:junit:4.12'
	
	// challonge-java SNAPSHOT
	implementation group: 'at.stefangeyer.challonge', name: 'core', version: '3.0.0-SNAPSHOT'
	// needed for tests SNAPSHOT
	testImplementation group: 'at.stefangeyer.challonge.serializer', name: 'gson', version: '3.0.0-SNAPSHOT'
	testImplementation group: 'at.stefangeyer.challonge.rest', name: 'retrofit', version: '3.0.0-SNAPSHOT'
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}
				
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}
			
			pom.project {
				name 'ChallongeListener'
				packaging 'jar'
				description 'A project using the stefangeyer/challonge-java project to fire events to listeners when a Challonge tournament changes.'
				url 'http://githb.com/gpluscb/ChallongeListener'
				
				scm {
					connection 'scm:git:git://github.com/gpluscb/ChallongeListener.git'
					developerConnection 'scm:git:git://github.com/gpluscb/ChallongeListener.git'
					url 'https://github.com/gpluscb/ChallongeListener'
				}
				
				licenses {
					license {
						name = 'The MIT License'
						url = 'https://raw.githubusercontent.com/gpluscb/ChallongeListener/master/LICENSE'
					}
				}
				
				developers {
					developer {
						id 'gpluscb'
					}
				}
			}
		}
	}
}

signing {
	sign configurations.archives
}

javadoc {
	if (JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}

task sourceJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier 'javadoc'
	from javadoc
}

artifacts {	
	archives sourceJar
	archives javadocJar
}
